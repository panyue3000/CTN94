
/*TIME FRAME PAST 30 DAYS*/
/*INCLUDING BL AND FOLLOW UP*/

/*RASEXPRF */
/*HOW WOULD YOU DESCRIBE*/
/*YOURSELF?*/
/*1=STRAIGHT OR HETEROSEXUAL, 2=GAY OR HOMOSEXUAL, 3=BISEXUAL*/
/**/
/*RASEXMEN */
/*WITH HOW MANY MEN HAVE*/
/*YOU HAD SEX IN THE PAST*/
/*MONTH? 0=0, 1=1, 2=2 OR 3, 3=4 OR MORE*/


/*IN THE PAST MONTH, HOW OFTEN DID YOU USE CONDOMS WHEN YOU HAD SEX?(RASEXSFE*/
/*CTN51  ------ VS CTN30 AND 27*/
/**/
/*0=I HAVE NOT HAD SEX IN THE PAST MONTH, -- 96 AS NA OR MISSING NO SEX*/
/*1=ALL THE TIME,  -- 4 ALWAYS*/
/*2=MOST OF THE TIME, -- 3 MORE THAN HALF*/
/*3=SOME OF THE TIME,  -- 2 1 LESS THAN HALF AND ABOUT HALF*/
/*4=NONE OF THE TIME   -- 0 NEVER*/

PROC SQL;
   CREATE TABLE RBS51_01 AS 
   SELECT T1.PATID, 
          T1.VISNO, 
          INPUT(T1.RASEXPRF,BEST.) AS RASEXPRF,  /*1=STRAIGHT OR HETEROSEXUAL, 2=GAY OR HOMOSEXUAL, 3=BISEXUAL*/
          INPUT(T1.RASEXMEN,BEST.) AS RASEXMEN, /*0=0, 1=1, 2=2 OR 3, 3=4 OR MORE*/
          INPUT(T1.RASEXWMN,BEST.) AS RASEXWMN,
          INPUT(T1.RASEXSFE,BEST.) AS RASEXSFE,
          T1.RASEXPEN, 
          T1.RASEXUPR,
		  CASE WHEN T2.DEGENDER = '2' THEN 0
		  	   WHEN T2.DEGENDER = '1' THEN 1 END AS ISMALE
      FROM CTN51.RAB T1 LEFT JOIN CTN51.DEM T2 
		  ON T1.PATID=T2.PATID;
QUIT;

PROC FREQ DATA= RBS51_01;
TABLES  
	RASEXPRF
	RASEXMEN
	RASEXWMN
	RASEXSFE
	RASEXPEN
	RASEXUPR
	ISMALE
;
RUN;

/*CREATE SEX HARMANIZED VARIABLE*/

/*TXX_PRT TOTAL NUMBER OF PARTNER: 0=0, 1=1, 2=MORE THAN ONE*/
/*TXX_MPRT TOTAL MALE PARTNER*/
/*TXX_FPRT TOTAL FEMALE PARTNER*/
/**/
/*TXX_FRQ TOTAL # OF SEX*/
/*TXX_PXX TOTAL PROTECTIVE SEX(CONDOM USE)*/
/*TXX_UPX TOTAL UNPROTECTED SEX (TXX_FRQ - TXX_PXX)*/
/**/
/*MSW_NPT MSW # WOMEN PARTNER*/
/*MSW_FRQ MSW TOTAL # OF SEX*/
/*MSW_PXX MSW TOTAL # PROTECTED SEX*/
/*MSW_UPX MSW_FRQ-MSW_PXX*/
/**/
/*MSM_NPT */
/*MSM_FRQ */
/*MSM_PXX */
/*MSM_UPX */
/**/
/*WSX_NPT */
/*WSX_FRQ */
/*WSX_PXX */
/*WSX_UPX */

/*from ray*/
/*	do x = 1 to dim(usedDays);*/
/*		select;*/
/*			when(missing(usedDays[x])) days = .;*/
/*			when(usedDays[x] = "0") days = 0; * Not at all;*/
/*			when(usedDays[x] = "1") days = 5; * A few times;*/
/*			when(usedDays[x] = "2") days = 15; * A few times each week;*/
/*			when(usedDays[x] = "3") days = 30; * Everyday;*/
/*		end;*/
/*		daysIV = max(daysIV, coalesce(days,0));*/


PROC SQL;
   CREATE TABLE RBS51_02 AS 
   SELECT PATID AS WHO, 
          VISNO, 
		  /*Sex partner*/
		  SUM(RASEXMEN,RASEXWMN) AS TXX_PRT_0,
		  CASE WHEN CALCULATED TXX_PRT_0>1 THEN 2
		  	   ELSE CALCULATED TXX_PRT_0 END AS TXX_PRT,
		  CASE WHEN RASEXMEN>1 THEN 2
		  	   ELSE RASEXMEN END AS TXX_MPRT,
		  CASE WHEN RASEXWMN>1 THEN 2
		  	   ELSE RASEXWMN END AS TXX_FPRT,

		  /*Overall sex*/
	      RASEXPEN AS TXX_FRQ,
		  MAX((RASEXPEN - RASEXUPR),0) AS TXX_PXX,
		  RASEXUPR AS TXX_UPX,

		  /*MALE WITH NO MALE PARTNER AND ONLY WOMEN PARTNER AND CONSIDER STRAIGHT OR BISEXUAL*/
		  CASE WHEN ISMALE=1 AND RASEXMEN=0 AND RASEXWMN>0 AND RASEXPRF IN (1, 3) THEN 
		  	   CALCULATED TXX_FPRT END AS MSW_NPT,
		  CASE WHEN ISMALE=1 AND RASEXMEN=0 AND RASEXWMN>0 AND RASEXPRF IN (1, 3) THEN 
		  	   RASEXPEN END AS MSW_FRQ,
		  CASE WHEN ISMALE=1 AND RASEXMEN=0 AND RASEXWMN>0 AND RASEXPRF IN (1, 3) THEN 
		  	   MAX((RASEXPEN - RASEXUPR),0) END AS MSW_PXX,
		  CASE WHEN ISMALE=1 AND RASEXMEN=0 AND RASEXWMN>0 AND RASEXPRF IN (1, 3) THEN 
		  	   RASEXUPR END AS MSW_UPX,

		  /*MALE WITH MALE PARTNER AND CONSIDER GAY OR BISEXUAL*/
		  CASE WHEN ISMALE=1 AND RASEXMEN>0 AND RASEXPRF IN (2, 3) THEN 
		  	   CALCULATED TXX_MPRT END AS MSM_NPT,
		  CASE WHEN ISMALE=1 AND RASEXMEN>0 AND RASEXPRF IN (2, 3) THEN 
		  	   RASEXPEN END AS MSM_FRQ,
		  CASE WHEN ISMALE=1 AND RASEXMEN>0 AND RASEXPRF IN (2, 3) THEN 
		  	   MAX((RASEXPEN - RASEXUPR),0) END AS MSM_PXX,
		  CASE WHEN ISMALE=1 AND RASEXMEN>0 AND RASEXPRF IN (2, 3) THEN 
		  	   RASEXUPR END AS MSM_UPX,

		  /*FEMALE WITH EITHER PARTNER*/
		  CASE WHEN ISMALE=0 THEN 
		  	   CALCULATED TXX_PRT END AS WSX_NPT,
		  CASE WHEN ISMALE=0 THEN 
		  	   RASEXPEN END AS WSX_FRQ,
		  CASE WHEN ISMALE=0 THEN 
		  	   MAX((RASEXPEN - RASEXUPR),0) END AS WSX_PXX,
		  CASE WHEN ISMALE=0 THEN 
		  	   RASEXUPR END AS WSX_UPX,
		    RASEXPRF,
			RASEXMEN,
			RASEXWMN,
			RASEXSFE
		
      FROM RBS51_01
;
QUIT;


DATA RBS51;
SET RBS51_02;

IF VISNO = '00';

DROP 
RASEXPRF
RASEXMEN
RASEXWMN
RASEXSFE
;
RUN;